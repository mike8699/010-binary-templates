//------------------------------------------------
//--- 010 Editor v9.0.2 Binary Template
//
//      File: fixed_flow.bin
//   Authors: triabolical
//   Version: 1.00
//   Purpose: Parses fixed_flow.bin, a file that seems to control chapter to chapter progression
//  Category: File that control cutscenes, map, labels of a chapter. Can be used to add more chapters if there's room.
// File Mask: 
//  ID Bytes: 
//   History: 
//------------------------------------------------
LittleEndian();
#include "flow_enums.bt"
local int m = 0;
local int o = 0;
struct ParalogueBlock {
    int magic<bgcolor=cRed>; 
    int EntryCount<bgcolor=cRed>;
    int Size<bgcolor=cRed>;
    int Padding[13]<hidden=true>;
    for (m = 0; m < EntryCount; ++m){
        struct ParalogueEvent {
        Event postBattleCutscene<bgcolor=cYellow, name="Post Battle Cutscene">;
        Names ParalogueCharacter1<name="Paralogue Character">;
        Names ParalogueCharacter2<name="Paralogue Character">;
        Event preBattleCutscene<bgcolor=cYellow, name="Pre Battle Cutscene">;
        ScenarioName map<bgcolor=cGreen, name="Map ID">;
        byte unk<hidden=true>;
        }paralogue<read=EnumToString(map), name="Paralogue">;
    }
};
struct RouteFlow {
    int magic<bgcolor=cRed>;
    int EntryCount<bgcolor=cRed>;
    int Size<bgcolor=cRed>;
    int Padding[13]<hidden=true>;
    for (o = 0; o < EntryCount; ++o){
        struct Chapter {
            Event cutscene1<bgcolor=cYellow, name="Post Battle Cutscene", comment="Movies: 0-29, Narrations at 30, Cutscenes start at 120, Cindered Shadows occurs after support events">;
            Event cutscene2<bgcolor=cYellow, name="Post Battle Cutscene", comment="Movies: 0-29, Narrations at 30, Cutscenes start at 120, Cindered Shadows occurs after support events">;
            Event cutscene3<bgcolor=cYellow, name="Post Battle Cutscene", comment="Movies: 0-29, Narrations at 30, Cutscenes start at 120, Cindered Shadows occurs after support events">;
            Event cutscene4<bgcolor=cYellow, name="Post Battle Cutscene", comment="Movies: 0-29, Narrations at 30, Cutscenes start at 120, Cindered Shadows occurs after support events">;
            Event preChapterNarration<bgcolor=cGreen, name="Pre Battle Narration/Opening/Cutscene",comment="Movies: 0-29, Narrations at 30, Cutscenes start at 120, Cindered Shadows occurs after support events">;
            Event preBattleCutscene<bgcolor=cYellow, name="Pre Battle Cutscene", comment="Movies: 0-29, Narrations at 30, Cutscenes start at 120, Cindered Shadows occurs after support events">;
            Event cutscene<bgcolor=cYellow, name="Pre Battle Cutscene", comment="Movies: 0-29, Narrations at 30, Cutscenes start at 120, Cindered Shadows occurs after support events">;
            Event preBattleNarration<bgcolor=cGreen, name="Chapter Narration/Opening/Cutscene", comment="Movies: 0-29, Narrations at 30, Cutscenes start at 120, Cindered Shadows occurs after support events">;
            Event CutScene1<bgcolor=cGreen, name="Chapter Cutscene", comment="Movies: 0-29, Narrations at 30, Cutscenes start at 120, Cindered Shadows occurs after support events">;
            Event CutScene2<bgcolor=cGreen, name="Chapter Cutscene", comment="Movies: 0-29, Narrations at 30, Cutscenes start at 120, Cindered Shadows occurs after support events">;
            Event CutScene3<bgcolor=cGreen, name="Chapter Cutscene", comment="Movies: 0-29, Narrations at 30, Cutscenes start at 120, Cindered Shadows occurs after support events">;
            Event CutScene4<bgcolor=cGreen, name="Chapter Cutscene", comment="Movies: 0-29, Narrations at 30, Cutscenes start at 120, Cindered Shadows occurs after support events">;
            ubyte unk1;
            // Added file index for quick reference, DLC files in DATA1 from the DLC.
            bsi monastery<bgcolor=cBlue, name="Monastery Script", comment="Monastery BSI scripts in order, Skips the monastery if this is -1">;
            ubyte unk2;
            ubyte unk3;
            ubyte chapterLabel<bgcolor=cBlue,name="Chapter String Index">;
            Month MonthLabel<bgcolor=cBlue, name="Month", comment="Month on the calander">;
            ScenarioName map<bgcolor=cGreen, name="Map ID", comment="Matches Scenario Index in fixed_scenario">;
            ubyte unk4<name="Day?">;
            RouteName route<bgcolor=cRed, name="Route Label">;
            // Seems to be calander related
            for (m = 33; m < 40; ++m) ubyte unk;
        }chapter<name="Chapter", read=EnumToString(map)>;
    };
};

struct File {
    int count<bgcolor=cBlack>;
    struct PointerPair {
        int start<format=hex>;
        int length<format=hex>;
    } pointertable[count]<bgcolor=cRed>;
    local int i = 0;
        FSeek(pointertable[0].start);
        RouteFlow route<name="Route", read="Silver Snow">;
        FSeek(pointertable[1].start);
        RouteFlow route<name="Route",read="Azure Moon">;
        FSeek(pointertable[2].start);
        RouteFlow route<name="Route", read="Verdant Wind">;
        FSeek(pointertable[3].start);
        RouteFlow route<name="Route",read="Crimson Flower">;
        FSeek(pointertable[4].start);
        ParalogueBlock paralogues<name="Paralogues", read="Paralogue Block">;
        FSeek(pointertable[5].start);
        RouteFlow route<name="Route", read="Cindered Shadows">;
} file;